plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.3'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.liquibase.gradle' version '2.0.4'
}

group = 'com.vicarius'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

ext {
	set('springCloudVersion', "2021.0.3")
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.liquibase:liquibase-core'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	implementation 'com.google.code.gson:gson:2.10'
	testImplementation 'junit:junit:4.13.1'
	liquibaseRuntime("org.liquibase:liquibase-core:4.7.1")
	liquibaseRuntime group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
	liquibaseRuntime 'com.mysql:mysql-connector-j:8.0.32'
	liquibaseRuntime("org.liquibase.ext:liquibase-hibernate5:4.7.1")
	liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:3.0.0'
	liquibaseRuntime 'info.picocli:picocli:4.6.1'
	liquibaseRuntime 'org.springframework.boot:spring-boot-starter-data-jpa'
	liquibaseRuntime 'com.vladmihalcea:hibernate-types-52:2.20.0'
	liquibaseRuntime 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	liquibaseRuntime sourceSets.main.output
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j:8.0.32'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'com.h2database:h2'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
}

apply plugin: "org.liquibase.gradle"

tasks.named('test') {
	useJUnitPlatform()
}

diff.dependsOn compileJava
diffChangeLog.dependsOn compileJava
generateChangelog.dependsOn compileJava

liquibase {
	activities {
		main {
			Properties properties = new Properties()
			def propertiesFile = project.rootProject.file('.env.local')
			def timestamp = new Date().format('yyyyMMddHHmmssSSS', TimeZone.getTimeZone('GMT'))
			def urlDB
			def usernameDB
			def passwordDB
			String description = providers.gradleProperty('description').isPresent() ? "__" + providers.gradleProperty('description').get() : ""
			description = description.replace(" ", "").replace("-", "_")
			String gitBranch =""
			def isNotLocal = providers.gradleProperty('isLocal').present && providers.gradleProperty('isLocal').get().equalsIgnoreCase("No")
			if (!isNotLocal) {
				gitBranch = "git branch --show-current".execute().text.trim()
				gitBranch = gitBranch.replace("feature/", "").replace("bug/", "").replace("-", "_").toUpperCase()
			}

			def fileName = "src/main/resources/db/changelog/" + timestamp + description + "_for_" + gitBranch + ".mysql.sql"


			if (propertiesFile.exists()) {
				properties.load(propertiesFile.newDataInputStream())

				def host = properties.getProperty('VICARIUS_DATABASE_HOSTNAME')
				def schema = properties.getProperty('VICARIUS_DATABASE_SCHEMA')
				usernameDB = properties.getProperty('VICARIUS_DATABASE_USERNAME')
				passwordDB = properties.getProperty('VICARIUS_DATABASE_PASSWORD')

				urlDB = "jdbc:mysql://" + host + ":3306/" + schema

			}

			def taskNames = project.gradle.startParameter.taskNames

			if (taskNames.contains("rollback") || taskNames.contains("rollbackCount")) {

				fileName = "classpath:db/changelog-master.yml"
			}

			changeLogFile fileName
			driver "com.mysql.cj.jdbc.Driver"
			url urlDB
			username usernameDB
			password passwordDB
			diffTypes 'catalogs,tables,functions,views,columns,indexes,foreignkeys,\n' +
					'primarykeys,uniqueconstraints,data,storedprocedures,triggers,\n' +
					'sequences,databasepackage,databasepackagebody'

			referenceUrl "hibernate:spring:com.vicarius?dialect=org.hibernate.dialect.MySQL8Dialect&hibernate.physical_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringPhysicalNamingStrategy&hibernate.implicit_naming_strategy=org.springframework.boot.orm.jpa.hibernate.SpringImplicitNamingStrategy"
			referenceDriver "liquibase.ext.hibernate.database.connection.HibernateDriver"

		}
	}
}
